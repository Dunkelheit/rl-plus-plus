#CLASS {RL_INVENTORY} {kill};
#CLASS {RL_INVENTORY} {open};

/**********************************************************************************************************************
 * Module RL_INVENTORY                                                                                                *
 *                                                                                                                    *
 * Inventory stuff.                                                                                                   *
 **********************************************************************************************************************/

/register_module {RL_INVENTORY} {Inventario} {Cosas para tu inventario.};

/**********************************************************************************************************************
 * Functions                                                                                                          *
 **********************************************************************************************************************/

/*
 * Function @split_inventory_line{arg}
 *
 * Splits the text in an inventory line.
 *
 * Arguments:
 *   %1 (number) an inventory line
 *
 * Example:
 *   @split_inventory_line{Collar de Sirena, Espiral Rosa, Gargantilla de Eog y Amuleto de Voluntad};
 */
#FUNCTION {split_inventory_line}
{
    #VAR {result} {%1};
    #REPLACE {result} { y } {;};
    #REPLACE {result} {, } {;};
};

#FUNCTION {color_value} {
    #IF {%1 > 0} {
        #VAR {result} {<bfb>%1<099>};
    };
    #ELSEIF {%1 < 0} {
        #VAR {result} {<fbb>%1<099>};
    };
    #ELSE {
        #VAR {result} {%1};
    }
};

#FUNCTION {get_item_details} {
    #IF {"$rl_armory[$item]" != ""} {
        #FORMAT {line} {  %-25.25s %+3s %+3s %+3s %+3s %+3s } {$item} 
            {@color_value{$rl_armory[$item][bo]}} {@color_value{$rl_armory[$item][be]}} 
            {@color_value{$rl_armory[$item][bp]}} {@color_value{$rl_armory[$item][encantamiento]}}
            {@color_value{$rl_armory[$item][proteccion]}};
        #IF {("$location" == "Mano derecha" || "$location" == "Mano izquierda") && $rl_armory[$item][daño_minimo] && $rl_armory[$item][daño_media] && $rl_armory[$item][daño_maximo]} {
            #VAR {line} {$line <fac>$rl_armory[$item][daño_minimo]/$rl_armory[$item][daño_media]/$rl_armory[$item][daño_maximo]<099>};
        };
        #ELSEIF {"$location" == "Empuñando"} {
            #IF {$rl_armory[$item][daño_minimo] && $rl_armory[$item][daño_media] && $rl_armory[$item][daño_maximo]} {
                #VAR {line} {$line <fac>$rl_armory[$item][daño_minimo]/$rl_armory[$item][daño_media]/$rl_armory[$item][daño_maximo]<099>};
            };
            #ELSEIF {$rl_armory[$item][minimo_2manos] && $rl_armory[$item][media_2manos] && $rl_armory[$item][maximo_2manos]} {
                #VAR {line} {$line <fac>$rl_armory[$item][minimo_2manos]/$rl_armory[$item][media_2manos]/$rl_armory[$item][maximo_2manos]<099>};
            };
        };
        #IF {"$rl_armory[$item][efectos]" != ""} {
            #FOREACH {$rl_armory[$item][efectos][]} {effect} {
                #FORMAT {line} {%s <fca>%.4s %s<099>} {$line} {$effect} 
                    {$rl_armory[$item][efectos][$effect]};
            };
        };
        #FOREACH {fue;des;con;int;sab;car} {characteristic} {
            #IF {$rl_armory[$item][$characteristic]} {
                #FORMAT {line} {%s %.3s %s} {$line} {$characteristic} 
                    {@color_value{$rl_armory[$item][$characteristic]}};
            };
        };
        #FOREACH {ts-fuerza bruta;ts-veneno;ts-petrificacion;ts-agilidad;ts-paralizacion;ts-mental;ts-aliento;ts-conjuro;ts-artefacto;ts-enfermedad;ts-proteccion;ts-muerte;ts-horror} {saving} {
            #IF {$rl_armory[$item][$saving]} {
                #VAR {pretty_saving} {$saving};
                #REPLACE {pretty_saving} {ts-} {⚀};
                #FORMAT {line} {%s %.4s %s} {$line} {$pretty_saving} 
                    {@color_value{$rl_armory[$item][$saving]}};
            };
        };
        #FOREACH {acido;divino;enfermedad;magico;veneno;bien;mal;aire;electrico;frio;mental;agua;fuego;tierra} {resistence} {
            #IF {$rl_armory[$item][$resistence]} {
                #FORMAT {line} {%s %.3s %s} {$line} {$resistence} 
                    {@color_value{$rl_armory[$item][$resistence]}};
            };
        };
        #FOREACH {critico;evasion;sigilar;esconderse} {combat_stat} {
            #IF {$rl_armory[$item][$combat_stat]} {
                #FORMAT {line} {%s %.3s %s} {$line} {$combat_stat} 
                    {@color_value{$rl_armory[$item][$combat_stat]}};
            };
        };
        #IF {$rl_armory[$item][bono_ahuyentar]} {
            #FORMAT {line} {%s %.3s %s} {$line} {ahuyentar} 
                {@color_value{$rl_armory[$item][bono_ahuyentar]}};
        };
        #IF {$rl_armory[$item][bono_comandar]} {
            #FORMAT {line} {%s %.3s %s} {$line} {comandar} 
                {@color_value{$rl_armory[$item][bono_comandar]}};
        };
        #IF {$rl_armory[$item][poder_magico]} {
            #FORMAT {line} {%s %.3s %s} {$line} {pmagico} 
                {@color_value{$rl_armory[$item][poder_magico]}};
        };
        #IF {$rl_armory[$item][poder_curacion]} {
            #FORMAT {line} {%s %.3s %s} {$line} {pcuracion} 
                {@color_value{$rl_armory[$item][poder_curacion]}};
        };
        #VAR {result} {$line};
    };
    #ELSE {
        #VAR {result} {  $item};
    };
};

/**********************************************************************************************************************
 * Aliases                                                                                                            *
 **********************************************************************************************************************/

/*
 * Alias /si
 *
 * Extend the current inventory with armory data.
 *
 * Example:
 *   /si
 */
#ALIAS {/si} {
    #CLASS {RL_INVENTORY_IMPL} {open};
    #VAR {all_the_inventory} {};
    #EVENT {RECEIVED LINE} {
        #IF {"%%1" == "> "} {
            #REGEXP {$all_the_inventory} {* Cabeza:  %*.} {#VAR {rl_equipment[Cabeza]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Orejas:  %*.} {#VAR {rl_equipment[Orejas]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Cuello:  %*.} {#VAR {rl_equipment[Cuello]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Hombros: %*.} {#VAR {rl_equipment[Hombros]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Pecho:   %*.} {#VAR {rl_equipment[Pecho]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Espalda: %*.} {#VAR {rl_equipment[Espalda]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Brazos:  %*.} {#VAR {rl_equipment[Brazos]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Manos:   %*.} {#VAR {rl_equipment[Manos]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Dedos:   %*.} {#VAR {rl_equipment[Dedos]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Cintura: %*.} {#VAR {rl_equipment[Cintura]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Piernas: %*.} {#VAR {rl_equipment[Piernas]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {* Pies:    %*.} {#VAR {rl_equipment[Pies]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {Empu{ñ|ny}ando: %*.} {#VAR {rl_equipment[Empuñando]} {@split_inventory_line{&2}}};
            #REGEXP {$all_the_inventory} {Mano derecha:   %*.} {#VAR {rl_equipment[Mano derecha]} {@split_inventory_line{&1}}};
            #REGEXP {$all_the_inventory} {Mano izquierda: %*.} {#VAR {rl_equipment[Mano izquierda]} {@split_inventory_line{&1}}};
            
            #SHOWME {<AAB><138>@center{Superinventario}<099>\n};
            #FORMAT {line} {<acf>  %-25.25s %+3s %+3s %+3s %+3s %+3s  %s<099>} {} {BO} {BE} {BP} {Enc} {Pro} {Otras propiedades};
            #SHOWME {$line};
            #FOREACH {$rl_equipment[]} {location} {
                #SHOWME {<178>$location<099>};
                #FOREACH {$rl_equipment[$location]} {item} {
                    #IF {"$location" == "Empuñando"} {
                        #REGEXP {$rl_equipment[Empuñando]} {%* [%* [%*]]} {
                            #VAR {item} {&1};
                            #SHOWME {@get_item_details{}};
                            #VAR {item} {&2};
                            #SHOWME {@get_item_details{}};
                        } {
                            #REGEXP {$rl_equipment[Empuñando]} {%* [%*]} {
                                #VAR {item} {&1};
                                #SHOWME {@get_item_details{}};
                                #VAR {item} {&2};
                                #SHOWME {@get_item_details{}};
                            };
                        };
                    };
                    #ELSE {
                        #SHOWME {@get_item_details{}};
                    };
                };
            };
            #UNVAR {line};
            #UNVAR {pretty_saving};
            #UNVAR {all_the_inventory};
            #UNVAR {rl_equipment};
            #UNVAR {item};
            #UNVAR {saving};
            #UNVAR {location};
            #UNVAR {combat_stat};
            #UNVAR {resistence};
            #UNVAR {characteristic};
            #UNVAR {effect};
            #CLASS {RL_INVENTORY_IMPL} {kill};
        };
        #ELSE {
            #VAR {all_the_inventory} {$all_the_inventory %%1};
        };
        #LINE {gag};
    }; 
    #SEND {inventario};
    #CLASS {RL_INVENTORY_IMPL} {close};
}

/**********************************************************************************************************************
 * Autocompletion                                                                                                     *
 **********************************************************************************************************************/

#TAB {/si};

/**********************************************************************************************************************
 * Testing                                                                                                            *
 **********************************************************************************************************************/

#IF {$_development == 1} {
    #ALIAS {/test_RL_INVENTORY} {
        #SHOWME {No tests implemented.};
    };
};

#CLASS {RL_INVENTORY} {close};
