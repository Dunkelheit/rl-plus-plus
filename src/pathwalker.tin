#CLASS {RL_PATHWALKER} {kill};
#CLASS {RL_PATHWALKER} {open};

#ALIAS {/rumbo} {
    #CLASS {RL_PATHWALKER_IMPL} {open};
    #IF {"%1" == ""} {
        @message{Sint치xis: /rumbo <direcci칩n>|off};
        #RETURN;
    };
    #IF {"%1" == "off"} {
        #CLASS {RL_PATHWALKER_IMPL} {kill};
        @message{Rumbo detenido.};
        #RETURN;
    };
    #VAR {lastVisitedExit} {@oppositeDirection{%1}};
    #ACT {^%%* [{[nose,|]+[nose|]*}]{[ ]*}$} {
        #VARIABLE {exits} {%%2};
        #REPLACE {exits} {,} {;};
        #LIST {exits} {create} {$exits};
        #LIST {exits} {size} {exitsLength};
        #IF {$exitsLength > 2} {
            @message{Has llegado a una bifurcaci칩n en el camino.};
            #CLASS {RL_PATHWALKER_IMPL} {kill};
        };
        #ELSEIF {$exitsLength < 2} {
            @message{Has llegado a un callej칩n sin salida.};
            #CLASS {RL_PATHWALKER_IMPL} {kill};
        };
        #ELSE {
            #FOREACH {$exits[%%*]} {exit} {
                #IF {"$exit" != "$lastVisitedExit"} {
                    #VAR {lastVisitedExit} {@oppositeDirection{$exit}};
                    #DELAY {1} {$exit};
                    #BREAK;
                };
            };
        };
    };
    #ACT {^Parece que %* no produjo efecto alguno.$} {
        @message{Deteniendo el rumbo, pues algo raro ha sucedido.};
        #CLASS {RL_PATHWALKER_IMPL} {kill};
    };
    @message{Caminando rumbo %1...};
    %1;
    #CLASS {RL_PATHWALKER_IMPL} {close};
}

#CLASS {RL_PATHWALKER} {close};

