#CLASS {RL_MARKSMAN} {kill};
#CLASS {RL_MARKSMAN} {open};

/**********************************************************************************************************************
 * Module RL_MARKSMAN                                                                                                 *
 *                                                                                                                    *
 * Functionalities for the marksman class.                                                                            *
 **********************************************************************************************************************/

/register_module {RL_MARKSMAN} {Clases: Tirador} {Utilidades para la clase tirador.};

/**********************************************************************************************************************
 * Actions                                                                                                            *
 **********************************************************************************************************************/

#ACTION {^¡Lanzas un disparocertero contra %*!$} {
    #UNDELAY {cooldown_disparocertero_npcs};
    #UNDELAY {cooldown_disparocertero_pjs};
    #NOP Pongo un segundo mas porque el cooldown original a veces no es fiable al 100%;
    #DELAY {cooldown_disparocertero_npcs} {
        /message_cooldown {Disparo certero (NPCs)};
    } {12};
    #DELAY {cooldown_disparocertero_pjs} {
        /message_cooldown {Disparo certero (PJs)};
    } {5};
};

#ACTION {# ¡Descargas una salva de %*} {
    #UNDELAY {cooldown_salva_npcs};
    #UNDELAY {cooldown_salva_pjs};
    #NOP Pongo un segundo mas porque el cooldown original a veces no es fiable al 100%;
    #DELAY {cooldown_salva_npcs} {
        /message_cooldown {Salva (NPCs)};
    } {21};
    #DELAY {cooldown_disparocertero_pjs} {
        /message_cooldown {Salva (PJs)};
    } {11};
};

#ACTION {# ¡Impactas a %* con un fugaz disparo incapacitador que le atraviesa %*!$} {
    #UNDELAY {cooldown_disparoincapacitador_npcs};
    #UNDELAY {cooldown_disparoincapacitador_pjs};
    #NOP Pongo un segundo mas porque el cooldown original a veces no es fiable al 100%;
    #DELAY {cooldown_disparoincapacitador_npcs} {
        /message_cooldown {Disparo incapacitador (NPCs)};
    } {61};
    #DELAY {cooldown_disparoincapacitador_pjs} {
        /message_cooldown {Disparo incapacitador (PJs)};
    } {31};
};

#ACTION {T{ú|u} y tu %* os fund{í|i}s en uno %*} {
    #CLASS {RL_MARKSMAN_DA_IMPL} {open};
    #VAR {counter_disparoapuntado} {0};
    #ALIAS {/clear_marksman_da_impl} {
        #UNVAR {counter_disparoapuntado};        
        #UNVAR {damage_bonus};
        #UNVAR {offensive_bonus};
        #CLASS {RL_MARKSMAN_DA_IMPL} {kill};
        #UNDELAY {cooldown_disparoapuntado_npcs};
        #UNDELAY {cooldown_disparoapuntado_pjs};
        #NOP Pongo un segundo mas porque el cooldown original a veces no es fiable al 100%;
        #DELAY {cooldown_disparoapuntado_npcs} {
            /message_cooldown {Disparo apuntado (NPCs)};
        } {21};
        #DELAY {cooldown_disparoapuntado_pjs} {
            /message_cooldown {Disparo apuntado (PJs)};
        } {11};
        #UNALIAS {/clear_marksman_da_impl};
    };    
    #ACTION {^Contin{ú|u}as preparando tu disparo perfecto contra %*.$} {
        #MATH {counter_disparoapuntado} {$counter_disparoapuntado + 1};
        #IF {$counter_disparoapuntado < 9} {
            #MATH {damage_bonus} {$counter_disparoapuntado * 14 * $rl_player[stats][weighted_level]};
            #MATH {offensive_bonus} {$counter_disparoapuntado * 10};
            #DELAY {0} {#SHOWME {<068>Turno: <138>$counter_disparoapuntado<068>, bonificaciones: daño <138>+$damage_bonus<068>, BO <138>+$offensive_bonus<099>}};
        };
        #ELSE {
            #DELAY {0} {#SHOWME {<138>¡Último turno! ¡dispara ya!<068> Bonificaciones: daño <138>+$damage_bonus<068>, BO <138>+$offensive_bonus<099>}};
        };
    };
    #ACTION {^¡Finalmente, liberas contra %* el disparo apuntado que tanto hab{í|i}as preparado!$} {
        /clear_marksman_da_impl;
    };
    #ACTION {^Tus ojos te duelen de no parpadear y no eres capaz de aguantar m{á|a}s la respiración...} {
        /clear_marksman_da_impl;
    };
    #CLASS {RL_MARKSMAN_DA_IMPL} {close};
};

/**********************************************************************************************************************
 * Highlights                                                                                                         *
 **********************************************************************************************************************/

#HIGHLIGHT {^Preparas tu %* y te dispones a disparar contra %*.$} {cyan bold};
#HIGHLIGHT {^¡Lanzas un disparocertero contra %*!} {green bold};

#HIGHLIGHT {^Colocas un mont{ó|o}n de %* en el suelo delante de ti %*} {cyan bold};
#HIGHLIGHT {^# ¡Descargas una salva de %*} {green bold}; 

#HIGHLIGHT {^# ¡Impactas a %* con un fugaz disparo incapacitador que le atraviesa %*!$} {green bold};

#HIGHLIGHT {^T{ú|u} y tu %* os fund{í|i}s en uno %*} {cyan bold};
#HIGHLIGHT {^Continúas preparando tu disparo perfecto contra %*.$} {cyan bold};

#HIGHLIGHT {^¡Finalmente, liberas contra %* el disparo apuntado que tanto hab{í|i}as preparado!$} {green bold};

#NOP # Arremetes contra Lobo asestándole un temible golpe crítico en el hocico.;
#NOP ¡Tu ataque clava un proyectil en el hocico de Lobo, causándole una profunda herida!;
#NOP # Sometes a Liebre a una poderosa ráfaga mágica crítica.;

#NOP # Abandonas tu estado de concentración bañada en un sudor frío, descubriendo heridas de las que no te habías percatado.;

/**********************************************************************************************************************
 * Testing                                                                                                            *
 **********************************************************************************************************************/

#IF {$_development == 1} {
    #ALIAS {/test_RL_MARKSMAN} {
        #SHOWME {No tests implemented.};
    };
};

#CLASS {RL_MARKSMAN} {close};
